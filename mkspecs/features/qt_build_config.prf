!contains(QMAKE_INTERNAL_INCLUDED_FILES, .*qmodule\\.pri) {
    QMAKE_QT_MODULE = $$[QT_HOST_DATA/get]/mkspecs/qmodule.pri
    !exists($$QMAKE_QT_MODULE)|!include($$QMAKE_QT_MODULE, "", true) {
        error("Cannot load qmodule.pri!")
    } else {
        debug(1, "Loaded qmodule.pri from ($$QMAKE_QT_MODULE)")
    }
} else {
    debug(1, "Not loading qmodule.pri twice")
}

mac {
   !isEmpty(QMAKE_RPATHDIR){
       CONFIG += absolute_library_soname
   }
}

# Qt modules get compiled without exceptions enabled by default.
# However, testcases should be still built with exceptions.
CONFIG += exceptions_off testcase_exceptions
